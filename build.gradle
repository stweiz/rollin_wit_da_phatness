buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.1.0.RELEASE"
    }
}

plugins {
    id 'com.bmuschko.docker-remote-api' version '4.0.3'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

description = "Rollin wit da phatness"
group = 'org.stweiz'
version = '1'

sourceCompatibility = JavaVersion.VERSION_1_10
targetCompatibility = JavaVersion.VERSION_1_10

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-undertow'
    compile 'org.springframework.data:spring-data-jpa'
    compile 'org.springframework.security:spring-security-web'
    compile 'org.springframework.security:spring-security-config'

    runtime 'org.postgresql:postgresql'
}


configurations {
    all {
        compile {
            exclude module: 'spring-boot-starter-tomcat'
            exclude group: 'org.apache.tomcat'
        }
    }
}

jar {
    manifest {
        attributes(
            'Implementation-Title': project.description,
            'Implementation-Version': project.version
        )
    }
}

springBoot {
    buildInfo()
}

// build docker image (see https://github.com/bmuschko/gradle-docker-plugin)
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

task createDockerfile(type: Dockerfile) {
    //destFile = project.file('build/docker/Dockerfile')
    from 'openjdk:10-jre-slim'
    label(['maintainer': 'stweiz <stef.weiz@googlemail.com>'])
    user '1000:1000'
    addFile(jar.archiveName, '/')
    environmentVariable('JAVA_OPTS', '""')
    entryPoint('sh', '-c', 'java $JAVA_OPTS -jar ' + jar.archiveName)
    exposePort 8080
}

task copyJarToDockerContext(type: Copy) {
    dependsOn bootJar
    from jar
    into 'build/docker/'
}

task buildDockerImage(type: DockerBuildImage) {
    dependsOn copyJarToDockerContext
    dependsOn createDockerfile
    inputDir = createDockerfile.destFile.get().asFile.parentFile
    tags.add("localhost/" + "$project.name:$project.version")
    doLast {
        println tags.get()[0]
    }
}
